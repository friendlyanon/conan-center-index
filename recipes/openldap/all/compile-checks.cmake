# ---- Autotools style compile checks ----

# Checks here were manually enumerated to be minimal and to only include
# checks affecting the ldap and lber libraries. These may change in the future.

include(CheckFunctionExists)
include(CheckIncludeFile)
include(CheckSymbolExists)
include(CheckTypeSize)

macro(to_define name)
  string(MAKE_C_IDENTIFIER "${name}" to_define_result)
  string(TOUPPER "${to_define_result}" to_define_result)
endmacro()

macro(check_include header)
  to_define("${header}")
  check_include_file("${header}" "HAVE_${to_define_result}")
endmacro()

macro(check_function function)
  to_define("${function}")
  check_function_exists("${function}" "HAVE_${to_define_result}")
endmacro()

macro(set_if condition var value)
  if(${condition})
    set("${var}" "${value}")
  endif()
endmacro()

check_include(arpa/inet.h)
check_include(arpa/nameser.h)
check_include(assert.h)
check_include(bits/types.h)
check_include(crypt.h)
check_include(dirent.h)
check_include(errno.h)
check_include(fcntl.h)
check_include(getopt.h)
check_include(limits.h)
check_include(malloc.h)
check_include(memory.h)
check_include(ndir.h)
check_include(netinet/tcp.h)
check_include(io.h)
check_include(poll.h)
check_include(process.h)
check_include(psap.h)
check_include(pthread.h)
check_include(resolv.h)
check_include(sched.h)
check_include(sgtty.h)
check_include(stddef.h)
check_include(stdint.h)
check_include(stdlib.h)
check_include(strings.h)
check_include(string.h)
check_include(sysexits.h)
check_include(syslog.h)
check_include(sys/dir.h)
check_include(sys/errno.h)
check_include(sys/file.h)
check_include(sys/filio.h)
check_include(sys/ioctl.h)
check_include(sys/ndir.h)
check_include(sys/param.h)
check_include(sys/poll.h)
check_include(sys/select.h)
check_include(sys/socket.h)
check_include(sys/stat.h)
check_include(sys/syslog.h)
check_include(sys/time.h)
check_include(sys/types.h)
check_include(sys/uio.h)
check_include(sys/un.h)
check_include(sys/wait.h)
check_include(termios.h)
check_include(unistd.h)
check_include(time.h)
set_if("HAVE_TIME_H;AND;HAVE_SYS_TIME_H" TIME_WITH_SYS_TIME 1)
check_include(winsock2.h)
set_if(HAVE_WINSOCK2_H HAVE_WINSOCK2 1)
check_include(winsock.h)
set_if(HAVE_WINSOCK_H HAVE_WINSOCK 1)

check_function(bcopy)
check_function(closesocket)
check_function(ctime_r)
check_function(fcntl)
check_function(gai_strerror)
check_function(getaddrinfo)
check_function(getdtablesize)
check_function(geteuid)
check_function(gethostbyaddr_r)
check_function(gethostbyname_r)
check_function(getnameinfo)
check_function(getopt)
check_function(getpassphrase)
check_function(getpeereid)
check_function(getpeerucred)
check_function(gettimeofday)
check_function(gmtime_r)
check_function(hstrerror)
check_function(inet_ntoa_b)
check_function(inet_ntop)
check_function(localtime_r)
check_function(memcpy)
check_function(memmove)
check_function(memrchr)
check_function(mkstemp)
check_function(pipe)
check_function(poll)
check_function(pthread_getconcurrency)
check_function(pthread_kill)
check_function(pthread_kill_other_threads_np)
check_function(pthread_rwlock_destroy)
check_function(pthread_setconcurrency)
check_function(sendmsg)
check_function(sigaction)
check_function(sigset)
check_function(snprintf)
check_function(strdup)
check_function(strerror)
check_function(strerror_r)
check_function(strpbrk)
check_function(strrchr)
check_function(strspn)
check_function(strtoll)
check_function(strtoq)
check_function(strtoull)
check_function(strtouq)
check_function(sysconf)
check_function(vsnprintf)

foreach(
    pair
    caddr_t\;char\ *
    gid_t\;int
    mode_t\;int
    off_t\;long
    pid_t\;int
    size_t\;unsigned\ int
    ssize_t\;signed\ int
    uid_t\;int
)
  list(GET pair 0 type)
  list(GET pair 1 type_to_use)
  to_define("${type}")
  check_type_size("${type}" "${to_define_result}_TYPE")
  set_if("NOT;HAVE_${to_define_result}_TYPE" "${type}" "${type_to_use}")
endforeach()

check_type_size(ptrdiff_t PTRDIFF_T)

set(includes "")
set_if(WIN32 includes stdlib.h)
list(APPEND includes stdio.h sys/types.h errno.h)
check_symbol_exists(sys_errlist "${includes}" HAVE_SYS_ERRLIST)

foreach(type short int long long\ long)
  to_define("${type}")
  check_type_size("${type}" "${to_define_result}_TYPE" BUILTIN_TYPES_ONLY)
  if("${HAVE_${to_define_result}_TYPE}")
    set("SIZEOF_${to_define_result}" "${${to_define_result}_TYPE}")
    if(NOT DEFINED LBER_INT_T AND "${${to_define_result}_TYPE}" GREATER "3") # at least 32 bits
      set(LBER_INT_T "${type}")
      set(LBER_TAG_T "${type}")
      set(LBER_LEN_T "${type}")
    endif()
  endif()
endforeach()

check_type_size(wchar_t WCHAR_T)
set_if("HAVE_WCHAR_T;AND;WCHAR_T" SIZEOF_WCHAR_T "${WCHAR_T}")

set(CMAKE_EXTRA_INCLUDE_FILES time.h)
check_type_size("struct timespec" TIMESPEC_TYPE BUILTIN_TYPES_ONLY)
set(CMAKE_EXTRA_INCLUDE_FILES "")
set_if(HAVE_TIMESPEC_TYPE TIMESPEC_IN_TIME 1)
